program = { func+ ~ EOI }

func = { kw_rec? ~ kw_func ~ ident ~ refine_type* ~ colon ~ refine_type ~ paren_expr }

refine_type = { primary_refine_type ~ (arrow ~ primary_refine_type)* }
primary_refine_type = {
    refine_non_func_type |
    left_paren ~ refine_type ~ right_paren}

refine_non_func_type = { left_paren ~ ident ~ colon ~ base_type ~ bar ~ formula ~ right_paren }

formula = { and_formula ~ (or ~ and_formula)* }
and_formula = { imply_formula ~ (and ~ imply_formula)* }
imply_formula = { binop_formula ~ (fat_arrow ~ binop_formula)* }
binop_formula = {
    kw_true | kw_false |
    (additive_logical_expr ~ (eq | neq | leq | lt | geq | gt) ~ additive_logical_expr) |
    (left_paren ~ formula ~ right_paren) }
additive_logical_expr = { multive_logical_expr ~ ((plus | minus) ~ multive_logical_expr)* }
multive_logical_expr = { primary_logical_expr ~ ((ast | slash | percent) ~ primary_logical_expr)* }
primary_logical_expr = { ident | constant | left_paren ~ additive_logical_expr ~ right_paren }

base_type = { kw_int }

expr = {
    let_expr |
    or_expr }

let_expr = { kw_let ~ ident ~ equal ~ expr ~ kw_in ~ expr }

or_expr = { and_expr ~ (or ~ and_expr)* }
and_expr = { comp_expr ~ (and ~ comp_expr)* }
comp_expr = { additive_expr ~ ((eq | neq | lt | leq | gt | geq) ~ additive_expr)* }

additive_expr = { multive_expr ~ ((plus | minus) ~ multive_expr)* }
multive_expr = { apply_expr ~ ((ast | slash | percent) ~ apply_expr)* }

apply_expr = {
    (ident ~ left_paren ~ right_paren) |
    (ident ~ left_paren ~ expr ~ (comma ~ expr)* ~ right_paren) |
    primary_expr }

primary_expr = {
    ifz_expr |
    constant |
    variable |
    paren_expr
}

ifz_expr = { kw_ifz ~ expr ~ left_brace ~ expr ~ right_brace ~ kw_else ~ left_brace ~ expr ~ right_brace }
constant = @{ minus? ~ digit+ }
variable = { ident }
paren_expr = { left_paren ~ expr ~ right_paren }

left_paren = { "(" }
right_paren = { ")" }
left_brace = { "{" }
right_brace = { "}" }
plus = { "+" }
minus = { "-" }
ast = { "*" }
slash = { "/" }
percent = { "%" }
or = { "|" }
and = { "&" }
equal = { "=" }
eq = { "=" }
neq = { "<>" }
lt = { "<" }
leq = { "<=" }
gt = { ">" }
geq = { ">=" }
colon = { ":" }
comma = { "," }
bar = { "|" }
arrow = { "->" }
fat_arrow = { "=>" }

kw_ifz = { "ifz" }
kw_else = { "else" }
kw_let = { "let" }
kw_in = { "in" }
kw_rec = { "rec" }
kw_func = { "func" }
kw_int = { "int" }
kw_true = { "true" }
kw_false = { "false" }

keyword = { kw_ifz | kw_else | kw_let | kw_in | kw_rec | kw_func | kw_int | kw_true | kw_false }

alpha = {'a'..'z' | 'A'..'Z'}
digit = {'0'..'9'}

ident_tail = { alpha | digit | "_" }

ident = @{ !keyword ~ alpha ~ ident_tail* }

WHITESPACE = _{ " " | "\t" | "\n" }

inline_comment = _{ "//" ~ (!"\n" ~ ANY)* ~ "\n" }
multiline_comment = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

COMMENT = _{ inline_comment | multiline_comment }

